/****
SqlDataProvider script DNN_FAQ 05.00.00

Note: 
To manually execute this script you must  perform a search and replace operation for {databaseOwner} and {objectQualifier}  

***/

ALTER TABLE {databaseOwner}[{objectQualifier}FAQs]
ADD ViewOrder INT NOT NULL DEFAULT (0)

ALTER TABLE {databaseOwner}[{objectQualifier}FAQsCategory]
ADD FaqCategoryParentId INT NOT NULL DEFAULT (0)
GO

/* -------------------------------------------------------------------------------------
/   FaqAdd
/  ------------------------------------------------------------------------------------- */

if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}{objectQualifier}FAQAdd') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
DROP PROCEDURE {databaseOwner}[{objectQualifier}FAQAdd]
go

CREATE PROCEDURE {databaseOwner}[{objectQualifier}FAQAdd]
(
	@ModuleId int,
	@CategoryId int,
	@Question ntext,
	@Answer ntext,
	@CreatedByUser nvarchar,
	@CreatedDate datetime,
	@DateModified datetime,
	@ViewCount int,
	@ViewOrder int
)
AS

INSERT INTO {databaseOwner}[{objectQualifier}FAQs] (
	[ModuleId],
	[CategoryId],
	[Question],
	[Answer],
	[CreatedByUser],
	[CreatedDate],
	[DateModified],
	[ViewCount],
	[ViewOrder]
) VALUES (
	@ModuleId,
	@CategoryId,
	@Question,
	@Answer,
	@CreatedByUser,
	@CreatedDate,
	@DateModified,
	@ViewCount,
	@ViewOrder
)

select SCOPE_IDENTITY()
go

/* -------------------------------------------------------------------------------------
/   FAQUpdate
/  ------------------------------------------------------------------------------------- */
if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}{objectQualifier}FAQUpdate') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
DROP PROCEDURE {databaseOwner}[{objectQualifier}FAQUpdate]
go

CREATE PROCEDURE {databaseOwner}[{objectQualifier}FAQUpdate]
	@ItemId int, 
	@ModuleId int, 
	@CategoryId int, 
	@Question ntext, 
	@Answer ntext, 
	@CreatedByUser nvarchar, 
	@DateModified datetime,
	@ViewOrder int
AS

UPDATE {databaseOwner}[{objectQualifier}FAQs] SET
	[CategoryId] = @CategoryId,
	[Question] = @Question,
	[Answer] = @Answer,
	[CreatedByUser] = @CreatedByUser,
	[DateModified] = @DateModified,
	[ViewOrder] = @ViewOrder
WHERE
	[ModuleId] = @ModuleId and [ItemId] = @ItemId

GO




/* -------------------------------------------------------------------------------------
/   FAQGet
/  ------------------------------------------------------------------------------------- */
if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}{objectQualifier}FAQGet') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
DROP PROCEDURE {databaseOwner}[{objectQualifier}FAQGet]
go


CREATE PROCEDURE {databaseOwner}[{objectQualifier}FAQGet]
(
	@ItemId   int,
	@ModuleId int
)
AS
SELECT
	f.ItemID,
	f.ModuleID,
	f.CategoryId,
	f.Question,
	f.Answer,
	u.Displayname AS CreatedByUserName,
	f.CreatedDate,
	f.DateModified,
	f.ViewCount,
	f.ViewOrder,
	c.FaqCategoryName,
	c.FaqCategoryDescription
	
FROM {databaseOwner}[{objectQualifier}FAQs] f
LEFT OUTER JOIN {databaseOwner}[{objectQualifier}FAQsCategory] c on f.CategoryId = c.FaqCategoryId
LEFT OUTER JOIN {databaseOwner}[{objectQualifier}Users] u on f.CreatedByUser = u.UserId
where f.ModuleId = @ModuleId and
f.ItemId = @ItemId

GO

/* -------------------------------------------------------------------------------------
/   FaqSearch
/  ------------------------------------------------------------------------------------- */

if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}{objectQualifier}FAQSearch') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure {databaseOwner}[{objectQualifier}FAQSearch]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}FAQSearch]
(
	@ModuleId	int,
	@OrderBy	int
)
AS
SELECT
	f.ItemId,
	f.ModuleId,
	f.CategoryId,
	f.Question,
	f.Answer,
	f.CreatedByUser,
	u.Displayname AS CreatedByUserName,
	f.CreatedDate,
	f.DateModified,
	f.ViewCount,
	f.ViewOrder,
	c.FaqCategoryName,
	c.FaqCategoryDescription
FROM {databaseOwner}[{objectQualifier}FAQs] f
left outer join {databaseOwner}[{objectQualifier}FAQsCategory] c on f.CategoryId = c.FaqCategoryId
left outer join {databaseOwner}[{objectQualifier}Users] U on f.CreatedByUser = U.UserId
where f.ModuleId = @ModuleId
ORDER BY
  CASE WHEN @OrderBy=0 THEN f.DateModified END DESC,
  CASE WHEN @OrderBy=1 THEN f.DateModified END ASC, 
  CASE WHEN @OrderBy=2 THEN f.ViewCount END DESC,
  CASE WHEN @OrderBy=3 THEN f.ViewCount END ASC,
  CASE WHEN @OrderBy=4 THEN f.CreatedDate END DESC,
  CASE WHEN @OrderBy=5 THEN f.CreatedDate END ASC,
  CASE WHEN @OrderBy=6 THEN f.ViewOrder END ASC
GO

/* -------------------------------------------------------------------------------------
/   FAQReorder
/  ------------------------------------------------------------------------------------- */
if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}{objectQualifier}FAQReorder') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure {databaseOwner}[{objectQualifier}FAQReorder]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}FAQReorder]
(
	@ItemId1 int,
	@ItemId2 int,
	@ModuleId int
)
AS
/* Swap Vieworder From FAQ records with ItemId1 and ItemId2 */
WITH tmpSwappedOrder(ItemId,ViewOrder) AS
(
	SELECT @ItemId2 AS ItemId,ViewOrder FROM {databaseOwner}[{objectQualifier}FAQs] WHERE ItemId = @ItemId1
	UNION
	SELECT @ItemId1 AS ItemId,ViewOrder FROM {databaseOwner}[{objectQualifier}FAQs] WHERE ItemId = @ItemId2
)
UPDATE {databaseOwner}[{objectQualifier}FAQs] SET ViewOrder = (SELECT ViewOrder FROM tmpSwappedOrder s WHERE s.ItemId = {databaseOwner}[{objectQualifier}FAQs].ItemId)
WHERE {databaseOwner}[{objectQualifier}FAQs].ItemId IN (SELECT ItemId FROM tmpSwappedOrder);

/* Renumber ViewOrder in FAQs */
WITH tmpReorder(ViewOrder,ItemId) AS
(
	SELECT TOP 1000 row_number() OVER (ORDER BY f.ViewOrder) as rank, f.ItemId
	FROM {databaseOwner}[{objectQualifier}FAQs] f
	WHERE f.ModuleId = @ModuleId
	ORDER BY rank 
)
UPDATE {databaseOwner}[{objectQualifier}FAQs] 
	SET ViewOrder = (SELECT ViewOrder FROM tmpReorder r WHERE r.ItemId = {databaseOwner}[{objectQualifier}FAQs].ItemId)
	WHERE ModuleId = @ModuleId

GO

/* -------------------------------------------------------------------------------------
/   FaqCategoryAdd
/  ------------------------------------------------------------------------------------- */

if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}{objectQualifier}FAQCategoryAdd') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
DROP PROCEDURE {databaseOwner}[{objectQualifier}FAQCategoryAdd]
go

CREATE PROCEDURE {databaseOwner}[{objectQualifier}FAQCategoryAdd]
(
	@ModuleId int,
	@FaqCategoryParentId int,
	@FaqCategoryName nvarchar(100),
	@FaqCategoryDescription nvarchar(250)
)
AS
INSERT INTO {databaseOwner}[{objectQualifier}FAQsCategory] (
	[ModuleId],
	[FaqCategoryParentId],
	[FaqCategoryName],
	[FaqCategoryDescription]
) VALUES (
	@ModuleId,
	@FaqCategoryParentId,
	@FaqCategoryName,
	@FaqCategoryDescription
)

select SCOPE_IDENTITY()
go


/* -------------------------------------------------------------------------------------
/   FaqCategoryDelete
/  ------------------------------------------------------------------------------------- */

if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}{objectQualifier}FAQCategoryDelete') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
DROP PROCEDURE {databaseOwner}[{objectQualifier}FAQCategoryDelete]
go

CREATE PROCEDURE {databaseOwner}[{objectQualifier}FAQCategoryDelete]
(
	@FaqCategoryId int
)
AS

DELETE FROM {databaseOwner}[{objectQualifier}FAQsCategory]
WHERE
	[FaqCategoryId] = @FaqCategoryId

go

/* -------------------------------------------------------------------------------------
/   FaqCategoryGet
/  ------------------------------------------------------------------------------------- */

if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}{objectQualifier}FAQCategoryGet') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
DROP PROCEDURE {databaseOwner}[{objectQualifier}FAQCategoryGet]
go


CREATE PROCEDURE {databaseOwner}[{objectQualifier}FAQCategoryGet]
(
	@FaqCategoryId int,
	@moduleId int 
)
AS
SELECT
	[FaqCategoryId],
	[ModuleId],
	[FaqCategoryParentId],
	[FaqCategoryName],
	[FaqCategoryDescription]
FROM {databaseOwner}[{objectQualifier}FAQsCategory]
WHERE
	[FaqCategoryId] = @FaqCategoryId
	AND [moduleid]=@moduleId 

go

/* -------------------------------------------------------------------------------------
/   FaqCategoryList 
/  ------------------------------------------------------------------------------------- */

if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}{objectQualifier}FAQCategoryList') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
DROP PROCEDURE {databaseOwner}[{objectQualifier}FAQCategoryList]
go


CREATE PROCEDURE {databaseOwner}[{objectQualifier}FAQCategoryList]
(
	@ModuleID	int,
	@OnlyUsedCategories   bit
)
AS

SELECT
	[FaqCategoryId],
	[ModuleId],
	[FaqCategoryParentId],
	[FaqCategoryName],
	[FaqCategoryDescription]
FROM {databaseOwner}[{objectQualifier}FAQsCategory]
WHERE [ModuleId] = @ModuleId
AND ([FaqCategoryId] IN (SELECT CategoryId FROM {databaseOwner}[{objectQualifier}FAQs]) OR @OnlyUsedCategories=0)

go

/* -------------------------------------------------------------------------------------
/   FaqCategoryUpdate
/  ------------------------------------------------------------------------------------- */

if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}{objectQualifier}FaqCategoryUpdate') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
DROP PROCEDURE {databaseOwner}[{objectQualifier}FAQCategoryUpdate]
go


CREATE PROCEDURE {databaseOwner}[{objectQualifier}FAQCategoryUpdate]
(
	@FaqCategoryId int, 
	@ModuleId int, 
	@FaqCategoryParentId int,
	@FaqCategoryName nvarchar(100), 
	@FaqCategoryDescription nvarchar(250)
)
AS
UPDATE {databaseOwner}[{objectQualifier}FAQsCategory] SET
	[ModuleId] = @ModuleId,
	[FaqCategoryParentId] = @FaqCategoryParentId,
	[FaqCategoryName] = @FaqCategoryName,
	[FaqCategoryDescription] = @FaqCategoryDescription
WHERE
	[FaqCategoryId] = @FaqCategoryId

go


/*** Remove unused procedure ***/
if exists (select * from dbo.sysobjects where id = object_id(N'{databaseOwner}{objectQualifier}ListFAQ') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure {databaseOwner}[{objectQualifier}ListFAQ]
GO
